plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'

    id "com.diffplug.spotless" version "6.20.0"
}

apply from: 'versioning.gradle'
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.

    mavenLocal()

    repositories {
        maven {
            url "https://amblelabs.github.io/maven"
            content {
                includeGroup "dev.amble"
            }
        }

        maven {
            url "https://theo.is-a.dev/maven/"
            content {
                includeGroup "dev.drtheo"
            }
        }
    }

    maven { url "https://api.modrinth.com/maven" }

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven { url "https://maven.blamejared.com/" }

    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
    }
    mavenLocal()
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }

    // YACL
    maven { url 'https://maven.isxander.dev/releases' }

	// IP
    maven {
		url "https://jitpack.io"
	}
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    include(implementation "net.objecthunter:exp4j:0.4.8")
    modImplementation "dev.drtheo:multidim:${project.multidim_version}"
    modApi "dev.emi:trinkets:${trinkets_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "dev.amble:ait:${ait_version}"
    modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"

    // getter setter
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    modImplementation("mezz.jei:jei-${project.minecraft_version}-fabric-api:${project.jei_version}")
    // Using Modrinth maven because JEI's official maven crashed with Yarn mappings
    modRuntimeOnly("maven.modrinth:jei:${project.jei_version}")
}


processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "yacl_version", project.yacl_version
    inputs.property "trinkets_version", project.trinkets_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "yacl_version": project.yacl_version,
                "trinkets_version": project.trinkets_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    // withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
			version = artifactVersion
            from components.java
        }
    }

    repositories {
        maven {
            name = "ci"
            url = providers.environmentVariable("CI_MAVEN_PATH")
        }
    }
}

fabricApi {
    configureDataGeneration {
        modId = "ait-extras"
		client = true
    }
}
